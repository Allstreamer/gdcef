#!python
###############################################################################
## Stigmee: The art to sanctuarize knowledge exchanges.
## Copyright 2021-2022 Quentin Quadrat <lecrapouille@gmail.com>
## Copyright 2021-2022 Alain Duron <duron.alain@gmail.com>
##
## This file is part of Stigmee.
##
## Stigmee is free software: you can redistribute it and/or modify it
## under the terms of the GNU General Public License as published by
## the Free Software Foundation, either version 3 of the License, or
## (at your option) any later version.
##
## This program is distributed in the hope that it will be useful, but
## WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
## General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with this program.  If not, see <http://www.gnu.org/licenses/>.
###############################################################################

import os

opts = Variables([], ARGUMENTS)
env = DefaultEnvironment()

# Define our options
platforms = ['windows', 'x11', 'linux', 'linuxbsd', 'macos', 'osx']
archis = ['x86_64', 'arm64']

# Define our options
opts.Add(EnumVariable('target', 'Compilation target', '', ['d', 'debug', 'r', 'release']))
opts.Add(EnumVariable('platform', 'Compilation platform', '', platforms + ['']))
opts.Add(EnumVariable('arch', 'Platform architecture', 'x86_64', ['x86_64', 'arm64']))
opts.Add(BoolVariable('use_llvm', 'Use the LLVM / Clang compiler', 'no'))
opts.Add(PathVariable('workspace', 'The path of Stigmee workspace', '', PathVariable.PathAccept))
opts.Add(PathVariable('godot_version', 'The Godot version installed in workspace', '3.4.2-stable', PathVariable.PathAccept))

# Updates the environment with the option variables.
opts.Update(env)

# Process some arguments.
# For the reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags
if env['use_llvm']:
    env['CC'] = 'clang'
    env['CXX'] = 'clang++'

# Process platform arguments. Here we use the same names as GDNative.
platform = env['platform']
if platform == 'macos':
    platform = 'osx'
elif platform in ('x11', 'linuxbsd'):
    platform = 'linux'
elif platform == 'bsd':
    platform = 'freebsd'
if platform == '':
    print('No valid target platform selected.')
    quit()

# Check if the Stigmee workspace is known
workspace = env['workspace']
if workspace == '':
    print('You shall define the path of the Stigmee workspace.')
    print('Please pass the content your environement variable WORKSPACE_STIGMEE')
    quit();
if not os.path.isdir(workspace):
    print('The argument workspace ' + workspace + ' path is invalid')
    print('Please pass the content your environement variable WORKSPACE_STIGMEE')
    quit();

# Check if Godot exists
godot_api_path = workspace + '/godot/' + env['godot_version'] + '/cpp'
if not os.path.isdir(godot_api_path):
    print('The Godot path ' + godot_api_path + ' is invalid.')
    print('Please pass the content your environement variable WORKSPACE_STIGMEE')
    quit();

# Compile either in release or debug mode
compile_mode = env['target']
if compile_mode == 'd':
    compile_mode = 'debug'
elif compile_mode == 'r':
    compile_mode = 'release'

# Compile CEF either in 'Release' or 'Debug' mode
cef_compile_mode = compile_mode.capitalize()

# Only support 64 at this time
# FIXME manage ARM
archi = '64'
if platform == 'osx':
    archi = 'x86_64'

# The godot library we want to create
target_path = workspace + '/stigmee/build'
target_library = 'libgdcef'

# Platform specific compilation
if platform == 'osx':
    env.Append(CXXFLAGS=['-std=c++17'])
    env.Append(LINKFLAGS=['-arch', 'x86_64', '-arch', 'arm64'])
    if compile_mode == 'debug':
        env.Append(CCFLAGS=['-g', '-O2'])
    else:
        env.Append(CCFLAGS=['-g', '-O3'])
    env.Append(CCFLAGS=['-fPIC'])
    env.Append(CXXFLAGS=['-DCEF_USE_SANDBOX', '-DNDEBUG',
                         '-DWRAPPING_CEF_SHARED', '-D__STDC_CONSTANT_MACROS',
                         '-D__STDC_FORMAT_MACROS'])

elif platform == 'linux':
    env.Append(CXXFLAGS=['-std=c++17'])
    if compile_mode == 'debug':
        env.Append(CCFLAGS=['-g3', '-Og'])
    else:
        env.Append(CCFLAGS=['-g', '-O3'])
    env.Append(CCFLAGS=['-fPIC'])
    env.Append(CXXFLAGS=['-DCEF_USE_SANDBOX', '-DNDEBUG', '-D_FILE_OFFSET_BITS=64',
                         '-D__STDC_CONSTANT_MACROS', '-D__STDC_FORMAT_MACROS'])

elif platform == 'windows':
    # This makes sure to keep the session environment variables on windows,
    # that way you can run scons in a vs 2017 prompt and it will find all the required tools
    env.Append(ENV=os.environ)
    env.Append(CPPDEFINES=['WIN32', '_WIN32', '_WINDOWS', '_CRT_SECURE_NO_WARNINGS'])
    env.Append(CCFLAGS=['-W3', '-GR'])
    env.Append(CXXFLAGS='/std:c++17')
    if compile_mode == 'debug':
        env.Append(CPPDEFINES=['_DEBUG'])
        env.Append(CCFLAGS=['-EHsc', '-MDd', '-ZI'])
        env.Append(LINKFLAGS=['-DEBUG'])
    else:
        env.Append(CPPDEFINES=['NDEBUG'])
        env.Append(CCFLAGS=['-O2', '-EHsc', '-MD'])

# Pathes of Godot header files
env.Append(CPPPATH=['.',
                    godot_api_path + '/godot-headers',
                    godot_api_path + '/include',
                    godot_api_path + '/include/core',
                    godot_api_path + '/include/gen'])

# Godot library that is linked against our library
libgodot = 'libgodot-cpp.' + platform + '.' + compile_mode + '.' + archi
env.Append(LIBS=[libgodot], LIBPATH=[godot_api_path + '/bin'])

# Pathes of CEF header files
cef_folder = '../thirdparty/cef_binary'
cef_include_folder = cef_folder + '/include'
env.Append(CPPPATH = [cef_folder,
                      cef_include_folder,
                      cef_include_folder + '/base',
                      cef_include_folder + '/base/internal',
                      cef_include_folder + '/capi',
                      cef_include_folder + '/capi/views',
                      cef_include_folder + '/internal',
                      cef_include_folder + '/views',
                      cef_include_folder + '/wrapper'])

# CEF libraries that are linked against our library
libcef = 'libcef' if platform != 'osx' else 'libcef_sandbox'
env.Append(LIBS = [libcef, 'libcef_dll_wrapper'],
           LIBPATH = [cef_folder + '/' + cef_compile_mode,
                      cef_folder + '/build',
                      cef_folder + '/build/libcef_dll_wrapper',
                      cef_folder + '/libcef_dll_wrapper/' + cef_compile_mode])

# Pathes header files of our module
env.Append(CPPPATH=['src/'])

# Compile the library
sources = Glob('src/*.cpp')
library = env.SharedLibrary(target=target_path + '/' + target_library, source=sources)
Default(library)

# Generates help for the -h scons option.
Help(opts.GenerateHelpText(env))
